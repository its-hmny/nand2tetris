class Main {
    static Grid grid;
    static Cursor cursor;
    static int player, key;
    
    function void main() {
        var String msg; let msg = "Player: X";

        let player = 1; let key = 0;
        let grid = Grid.new(2, 50); let cursor = Cursor.new(30);
        
        while (true) {
            do Screen.clearScreen(); do Screen.setColor(true);
            do grid.render(); do cursor.render();

            // Removes the placeholder and puts the actual player number
            do msg.eraseLastChar(); do msg.appendChar(player + 48);
            do Main.print(msg, 0); // Prints it on the screen (bottom)
            
            let key = Keyboard.keyPressed();
            if (key = 130) { do cursor.move(0, -1); }  // Left
            if (key = 131) { do cursor.move(-1, 0); }  // Up
            if (key = 132) { do cursor.move(0, 1); }   // Right
            if (key = 133) { do cursor.move(1, 0); }   // Down
            if (key = 128) { do Main.mark(); }         // Enter
            
            do Sys.wait(250);
        }

        return;
    }

    function void mark() {
        var int row, column;
        let row = cursor.getRow(); let column = cursor.getCol();
        if (~(grid.getCell(row, column) = 0)) {
            do Main.print("Cell already occupied", 1);
            return; 
        }

        do grid.setCell(row, column, player);
        
        if (player = 1) { let player = 2; }
        else { let player = 1; }

        return;
    }
    
    function void print(String msg, int line) {
        // Center coordinate of the Hack computer screen
        var int row, column; let row = 20; let column  = 32;

        do Output.moveCursor(row + line, column - (msg.length() / 2));
        do Output.printString(msg);
        
        return;

    }
}
